---
title: 03-Groovy高级用法
date: 2023-02-10 15:12:23
permalink: /pages/fc0c93/
tags: 
  - null
author: 
  name: CodeAshen
  link: https://github.com/codeashen
categories: 
  - 编程语言
  - Gradle
---


# 1 JSON 操作

Groovy 自带 json 操作工具类，不需要使用 Gson、fastJson 等依赖。

```groovy
def list = [new Person(name: "John", age: 30),
            new Person(name: "Jane", age: 25)]

// 对象转 json
def json = JsonOutput.toJson(list)
println JsonOutput.prettyPrint(json)

// json 转对象
def jsonSlurper = new JsonSlurper()
jsonSlurper.parseText(json)
```

如果需要使用第三方依赖，可以创建 lib 文件夹，将 jar 包拷贝进去，然后添加依赖，就可以在 Groovy 脚本中使用 jar 包中的 api 了。

![image-20220603063631987](https://cc.hjfile.cn/cc/img/20220603/2022060306363383581525.png)

# 2 XML 操作

## 2.1 XML 解析

**解析和读取**

```groovy
// 解析 XML
def slurper = new XmlSlurper()
def response = slurper.parseText(xml)

// 获取节点值
println response.value.books[0].book[0].title.text()  // 疯狂Android讲义
// 获取节点属性
println response.value.books[1].@classification       // web
```

**节点遍历普通方式**

```groovy
// 获取所有书籍名称
def titles = response.value.books.each { books -> 
    books.book.each { book -> 
        allBooks.add(book.title.text())
    }
}
```

**深度优先遍历**

```groovy
// 获取所有作者是李刚的书籍名称
def titles = response.depthFirst().findAll { node -> 
    node.author.text() == "李刚"
}.collect { node -> 
    node.title.text()
}

println titles  // [疯狂Android讲义, Vue从入门到精通]
```

**广度优先遍历**

```groovy
// 获取id=2的书籍名称
def titles = response.value.books.children().findAll { node -> 
    node.name() == "book" && node.@id == "2"
}.collect { node -> 
    node.title.text()
}

println titles  // [第一行代码]
```

## 2.2 生成 XML

希望生成一个 XML，内容如下：

```xml
<langs type='current' count='3' mainstream='true'>
  <language flavor='static' version='1.5'>Java</language>
  <language flavor='dynamic' version='1.6'>Groovy</language>
  <language flavor='dynamic' version='1.9'>JavaScript</language>
</langs>
```

生成代码：

```groovy
def sw = new StringWriter()
def xmlBuilder = new MarkupBuilder(sw)  // 用来生成 XML

xmlBuilder.langs(type: 'current', count: 3, mainstream: 'true') {
    language(flavor: 'static', version: '1.5', 'Java') 
    language(flavor: 'dynamic', version: '1.6', 'Groovy') 
    language(flavor: 'dynamic', version: '1.9', 'JavaScript') 
}

println sw
```

根据对象生成 XML

```groovy
class Language {
    String flavor
    String version
    String value
}

class Langs {
    String type = 'current'
    int count = 3
    boolean mainstream = true
    def languages = [
            new Language(flavor: 'static', version: '1.5', value: 'Java'),
            new Language(flavor: 'dynamic', version: '1.6', value: 'Groovy'),
            new Language(flavor: 'dynamic', version: '1.9', value: 'JavaScript')
    ]
}


def langs = new Langs()

xmlBuilder.langs(type: langs.type, count: langs.count, mainstream: langs.mainstream) {
    langs.languages.each { item ->
        language(flavor: item.flavor, version: item.version, item.value)
    }
}

println sw
```

# 3 文件操作

## 3.1 文本读写

```groovy
def file = new File("../../Hello Groovy.iml")

// 遍历文件行
file.eachLine { line ->
    println line
}

// 读取整个文件内容
def text = file.getText()

// 读取所有行
def lines = file.readLines()

// 读取文件前100个字符
def reader = file.withReader { reader ->
    char[] buffer = new char[100]
    reader.read(buffer)
    return buffer
}

/**
 * 拷贝文件
 * @param sourcePath 源文件路径
 * @param targetPath 目标文件路径
 * @return
 */
def copy(String sourcePath, String targetPath) {
    // 首先创建文件
    def targetFile = new File(targetPath)
    if (!targetFile.exists()) {
        targetFile.createNewFile()
    }
    // 拷贝文件
    new File(sourcePath).withReader { reader ->
        def lines = reader.readLines()
        targetFile.withWriter { writer ->
            lines.each { line ->
                writer.append(line)
            }
        }
    }
    return true
}
```

## 3.2 对象读写

```groovy
/**
 * 持久化对象
 * @param object 待持久化对象
 * @param path 持久化路径
 * @return
 */
def saveObject(Object object, String path) {
    def file = new File(path)
    if (!file.exists()) {
        file.createNewFile()
    }
    file.withObjectOutputStream { out ->
        out.writeObject(object)
    }
    return true
}

/**
 * 反序列化对象
 * @param path 反序列化路径
 * @return
 */
def readObject(String path) {
    def obj = null
    def file = new File(path)
    if (!file.exists()) {
        return null
    }
    file.withObjectInputStream { input ->
        obj.readObject()
    }
    return obj
}
```



















